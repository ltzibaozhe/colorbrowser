<?xml version="1.0" encoding="utf-8"?>
<mx:Panel xmlns:mx="http://www.adobe.com/2006/mxml" layout="vertical" title="Color Editor" xmlns:filters="flash.filters.*" xmlns:components="components.*">
	<mx:Script>
		<![CDATA[
			import mx.managers.PopUpManager;
			import com.sophware.Util;
			import mx.events.PropertyChangeEvent;
			import mx.collections.ArrayCollection;
			
			private var _colorObject:Object;
			private var browserWindow:ColorHandler;
			private var editSingle:SingleColor;
			public var editing:Boolean;
			[Bindable] private var _colorSet:ArrayCollection;
			[Bindable] private var _setName:String;
			[Bindable] private var _colorString:ArrayCollection;
			
			[Bindable]
			public function set colorObject(inObj:Object):void {
				_colorObject = inObj;
				_setName = inObj.name;
				_colorSet = new ArrayCollection(inObj.data.toArray());
			}
			public function get colorObject():Object {
				return _colorObject;
			}
			
			private function saveColor():void {
				var writeCollection:ArrayCollection = parentApplication.colorListCollection;
				var tempArray:Array = new Array();
				
				for(var i:int = 0; i< colorBox.length; i++) {
					tempArray.push(colorBox[i].colorVal);
				}
				
				if(editing) {
					var editIndex:Number = parentApplication.colorDisplayList.selectedIndex;
					writeCollection.removeItemAt(editIndex);
					writeCollection.addItemAt({name:setName.text,data:new ArrayCollection(tempArray)},editIndex);
					this.visible = false;
				}
				else {
					writeCollection.addItem({name:setName.text,data:new ArrayCollection(tempArray)});
					this.visible = false;
				}
			}
			
			private function addColor():void {
				_colorSet.addItem(0);
			}
			
			private function removeColor():void {
				_colorSet.removeItemAt(_colorSet.length-1);
			}
			
			private function updateColor(evt:Event):void {
				var tempArray:Array = new Array();
				
				for(var i:int = 0; i< colorBox.length; i++) {
					tempArray.push(colorBox[i].colorVal);
				}
				
				_colorSet = new ArrayCollection(tempArray);
			}
			
			private function showBrowser(evt:MouseEvent):void {
				editSingle = evt.target.parent as SingleColor;
				browserWindow = PopUpManager.createPopUp(this,ColorHandler,true) as ColorHandler;
				PopUpManager.centerPopUp(browserWindow);
				browserWindow.colorSample.colorVal = editSingle.colorVal;
				browserWindow.okbutton.addEventListener(MouseEvent.CLICK,handlerClick);
			}
			
			public function handlerClick(evt:MouseEvent):void {
				editSingle.colorVal = browserWindow.colorSample.colorVal;
				PopUpManager.removePopUp(browserWindow);
				updateColor(evt);
			}
		]]>
	</mx:Script>
	
	<mx:HBox>
		<mx:Label text="Name:" styleName="colorLabel" height="22">
			<mx:filters>
				<mx:DropShadowFilter angle="90" blurX="4" blurY="4" alpha="0.65" distance="1.5"/>
			</mx:filters>
		</mx:Label>
		<mx:TextInput id="setName" text="{_setName}"/>
	</mx:HBox>
	<mx:HBox verticalAlign="top">
		<mx:Tile width="350">
			<mx:Repeater id="colorRow" dataProvider="{_colorSet}" recycleChildren="true">
				<components:SingleColor id="colorBox" colorVal="{colorRow.currentItem as uint}" colorChange="updateColor(event)" doubleClickEnabled="true" doubleClick="showBrowser(event)"/>
			</mx:Repeater>
		</mx:Tile>
		<mx:VBox>
			<mx:Button label="+" click="addColor()" toolTip="Add New Color"/>
			<mx:Button label="-" click="removeColor()" toolTip="Remove Last Color"/>
		</mx:VBox>
	</mx:HBox>
	
	<mx:HBox width="100%" horizontalAlign="right">
		<mx:Button label="Ok" click="saveColor()"/>
		<mx:Button label="Cancel" click="{this.visible=false}"/>
	</mx:HBox>
</mx:Panel>
