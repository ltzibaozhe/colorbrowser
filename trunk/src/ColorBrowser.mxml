<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:comp="com.levitation.*" layout="absolute" xmlns:filters="flash.filters.*" xmlns:controls="flexlib.controls.*" applicationComplete="onComplete()" width="720" height="600">
	<mx:states>
		<mx:State name="singleView">
			<mx:SetProperty target="{singleView}" name="visible" value="true"/>
		</mx:State>
	</mx:states>
	<mx:Style source="css/colorswatch.css"/>
	<mx:Script>
		<![CDATA[
			import com.sophware.PaletteModel;
			import com.adobe.DeclarativeMenu;
			import mx.events.MenuEvent;
			import com.adobe.air.alert.AlertEvent;
			import com.adobe.air.alert.NativeAlert;
			import com.codeazur.utils.AIRRemoteUpdaterEvent;
			import com.codeazur.utils.AIRRemoteUpdater;
			import mx.events.CloseEvent;
			import mx.events.CollectionEvent;
			import com.sophware.Util;
			import flash.events.InvokeEvent;
			import com.levitation.ColorEdit;
			import mx.managers.CursorManager;
			import mx.events.DragEvent;
			import mx.events.ResizeEvent;
			import flash.filesystem.FileMode;
			import flash.filesystem.File;
			import flash.filesystem.FileStream;
			import flash.events.IOErrorEvent;
			import mx.events.FlexEvent;
			import mx.collections.ArrayCollection;
			import mx.core.Container;
			import mx.core.UIComponent;
			
			private var lastString:String;
			private var colorFile:File; //Save location for colors
			private var colorFileStream:FileStream;
			private var saveFile:File; //Save file reference
			private var saveFileStream:FileStream;
			private var inFile:File; //File to open, replaces current file
			private var inFileStream:FileStream;
			private var dragFile:File; //File dropped on app
			private var dragFileStream:FileStream;
			private var importFile:File; //Used for importing ASE files.
			
			private var updater:AIRRemoteUpdater;
			private var request:URLRequest = new URLRequest("http://www.levitationdesign.com/ColorBrowser.air");
			
			private var invokeVar:Boolean;
			
			//Variables to control filter dropshadows
			[Bindable]
			private var dropBlur:Number = 3;
			[Bindable]
			private var dropDistance:Number = 1;
			[Bindable]
			private var dropAlpha:Number = 0.65;
			
			[Bindable]
			private var currentPalette:PaletteModel;
			//private var colorCollection:ArrayCollection;
			[Bindable]
			public var colorListCollection:ArrayCollection = new ArrayCollection();
			
			private function onComplete():void {
				//Set up native menus
				var menus:DeclarativeMenu = new DeclarativeMenu(menuItems);
				menus.addEventListener(Event.SELECT,menuHandler);
				if(NativeApplication.supportsMenu) {
					NativeApplication.nativeApplication.menu = menus;
				}
				else if(NativeWindow.supportsMenu) {
					stage.nativeWindow.menu = menus;
				}
				
				//Set up file access
				colorFile = File.applicationStorageDirectory.resolvePath("saved.color");
				
				//File exists, use it
				if(colorFile.exists  && colorFile.size > 10) {
					colorFileStream = new FileStream();
					colorFileStream.open(colorFile, FileMode.UPDATE);
					colorListCollection = colorFileStream.readObject() as ArrayCollection;
				}
				//Need to create a new file and start using it
				else {
					var tempFileStream:FileStream = new FileStream();
					tempFileStream.open(colorFile,FileMode.WRITE);
					tempFileStream.close();
					colorFileStream = new FileStream();
					colorFileStream.open(colorFile, FileMode.UPDATE);
				}
				
				//Set up app to accept drag
				addEventListener(NativeDragEvent.NATIVE_DRAG_OVER,fileDragAccept);
				addEventListener(NativeDragEvent.NATIVE_DRAG_DROP,fileDragDrop);
				
				NativeApplication.nativeApplication.addEventListener(InvokeEvent.INVOKE,onInvokeEvent);
				
				//Listen for collection changes
				colorListCollection.addEventListener(CollectionEvent.COLLECTION_CHANGE,collectionUpdate);
				update();
			}
			
			private function menuHandler(evt:Event):void {
				switch(evt.target.label) {
					case "Import ASE":
						var imagesFilter:FileFilter = new FileFilter("Adobe Swatch Exchange (*.ase)", "*.ase");
						importFile = new File();
						importFile.browseForOpen("Select a file to import", [imagesFilter]);
						importFile.addEventListener(Event.SELECT, importFileSelected);
						break;
					case "Export ASE":
						trace("Export");
						break;
					case "Restore Collection":
						inFileClick();
						break;
					case "Backup Collection":
						outFileClick();
						break;
					default:
						trace("Fell out of menuHandler switch");
						trace(evt.target);
				}
			}
			
			//Handle updating via Codeazur's AIRUpdater
			public function update():void {
				updater = new AIRRemoteUpdater();
				updater.addEventListener(AIRRemoteUpdaterEvent.VERSION_CHECK, updaterVersionCheckHandler);
				updater.addEventListener(AIRRemoteUpdaterEvent.UPDATE, updaterUpdateHandler);
				updater.update(request);
			}
			
			protected function updaterVersionCheckHandler(evt:AIRRemoteUpdaterEvent):void {
				evt.preventDefault();
				var updater:AIRRemoteUpdater = evt.target as AIRRemoteUpdater;
				trace("Local version: " + updater.localVersion);
				trace("Remote version: " + updater.remoteVersion);
				if(updater.localVersion < updater.remoteVersion) {
					NativeAlert.show("Version "+updater.remoteVersion+" is available.  Would you like to download and install it?","New Version",NativeAlert.YES|NativeAlert.NO,true,this as NativeWindow,updaterClick);
				}
			}
			
			protected function updaterClick(evt:AlertEvent):void {
				if(evt.detail == NativeAlert.YES) {
					updater.update(request,false);
				}
				else {
					trace("You clicked NO");
				}
			}
			
			protected function updaterUpdateHandler(evt:AIRRemoteUpdaterEvent):void {
				trace("Installer: " + evt.file.nativePath);
			}
			
			//Referenced when a .color file is double clicked in the OS
			private function onInvokeEvent(evt:InvokeEvent):void {
				var tempFile:File;
				
				singleView.visible = false;
				
				for each(var tempString:String in evt.arguments) {
					try {
						tempFile = new File(tempString);
						if(tempFile.extension == "color") {
							//TODO: Use prompt when opening a file with the open button also.
							inFile = tempFile;
							NativeAlert.show("Do you want to save your current color set?","Alert",NativeAlert.YES|NativeAlert.NO,true,this as NativeWindow,handleFile);
						}
					}
					catch(err:Error) {
						trace(err.message);
					}
				}
			}
			
			private function handleFile(evt:AlertEvent):void {
				if(evt.detail == NativeAlert.YES) {
					invokeVar = true;
					outFileClick();
				}
				else {
					copyFile(new Event("bogus"));
				}
			}
			
			private function collectionUpdate(evt:CollectionEvent):void {
				writeCollection(colorListCollection);
			}
			
			private function renderColor(inPalette:Object):void {
				currentPalette = inPalette as PaletteModel;
				//colorCollection = inCollection.data as ArrayCollection;
			}
			
			private function setColor(inObject:Container,rgb:String):void {
				inObject.setStyle("backgroundColor",rgb);
			}
			
			private function addItem():void {
				editBox.visible = true;
				editBox.colorObject = {name:"New Color",data:new ArrayCollection([0,0,0,0,0])};
				editBox.editing = false;
			}
			
			private function deleteItem():void {
				colorListCollection.removeItemAt(colorDisplayList.selectedIndex);
				writeCollection(colorListCollection);
			}
			
			private function duplicateItem():void {
				colorListCollection.addItem(colorDisplayList.selectedItem.valueOf());
			}
			
			private function editItem():void {
				editBox.visible = true;
				editBox.colorObject = colorDisplayList.selectedItem;
				editBox.editing = true;
			}
			
			private function writeCollection(inCollection:ArrayCollection):void {
				colorFileStream.position = 0;
				colorFileStream.truncate();
				colorFileStream.writeObject(inCollection);
			}
			
			private function toClipBoard():void {
				System.setClipboard(colorDisplayList.selectedItem.string);
			}
			
			private function outFileClick():void {
				try {
					saveFile = File.documentsDirectory;
					saveFile.browseForSave("Save Colors");
					saveFile.addEventListener(Event.SELECT, saveColors);
				}
				catch (error:Error) {
					NativeAlert.show("Failed: "+error.message,"Save failed");
				}
			}
			
			private function saveColors(evt:Event):void {
				var outFile:File = evt.target as File;
				var outFileStream:FileStream = new FileStream();
				outFileStream.open(outFile,FileMode.WRITE);
				outFileStream.writeObject(colorListCollection);
				outFileStream.close();
				CursorManager.removeBusyCursor();
				if(invokeVar) {
					copyFile(new Event("bogus"));
				}
			}
			
			private function inFileClick():void {
				inFile = new File();
				inFile.addEventListener( Event.SELECT, copyFile );
				inFile.browse( new Array( new FileFilter( "Color Files (*.color)", "*.color" ) ) );
			}
			private function copyFile(evt:Event):void {
				colorFileStream.close();
				inFile.copyTo(colorFile,true);
				colorFileStream.open(colorFile, FileMode.UPDATE);
				colorListCollection = colorFileStream.readObject() as ArrayCollection;
			}
			
			private function closeApp():void {
				colorFileStream.close();
				stage.nativeWindow.close();
			}
			
			private function fileDragAccept(evt:NativeDragEvent):void {
				if(evt.clipboard.hasFormat(ClipboardFormats.FILE_LIST_FORMAT)){
					NativeDragManager.dropAction = NativeDragActions.COPY;
					NativeDragManager.acceptDragDrop(this);
				}
			}
			
			private function fileDragDrop(evt:NativeDragEvent):void {
				NativeDragManager.dropAction = NativeDragActions.COPY;
				var tempArray:Array = evt.clipboard.getData(ClipboardFormats.FILE_LIST_FORMAT) as Array;
				for each(var tempFile:File in tempArray) {
					if(tempFile.extension.toLowerCase() == "ase") {
						translateAse(tempFile);
					}
					else {
						NativeAlert.show("Sorry, I don't recognize that file format.");
					}
				}
			}
			
			//To use when translating an ASE file, currently very incomplete
			private function translateAse(inFile:File):void {
				trace("Path: "+inFile.nativePath);
				var fs:FileStream = new FileStream();
				fs.open(inFile,FileMode.READ);
				//trace(fs.bytesAvailable);
				
				var tempBytes:ByteArray = new ByteArray();
				fs.readBytes(tempBytes,0,fs.bytesAvailable);
				fs.close();
				
				var tempNum:Number = tempBytes.bytesAvailable;
				var colorSetArray:Array = new Array();
				
				for(var i:int=0; i< tempNum-3; i++) {
					tempBytes.position = i;
					//trace(tempBytes.position);
					if(tempBytes.readUTFBytes(3) == "RGB") {
						tempBytes.position++;
						trace(tempBytes.position.toString(16));
						trace("Start of color");
						
						var shortArray:Array = new Array();
						for(var j:int=0;j<3;j++) {
							shortArray.push(tempBytes.readFloat()*255);
						}
						var colorVal:uint = (shortArray[0]<<16) | (shortArray[1]<<8) | shortArray[2];
						trace(colorVal.toString(16));
						colorSetArray.push(colorVal);
						
						i = tempBytes.position-1;
					}
					
					editBox.visible = true;
					editBox.colorObject = {name:"New ASE Color",data:new ArrayCollection(colorSetArray)};
				}
			}
			
			private function importFileSelected(event:Event):void
			{
//				importFile = File(event.target);
				switch(importFile.extension.toLowerCase())
				{
				case "ase":
					translateAse(importFile);
					importFile = null;
					break;
				case "css":
//					translateColourLover(importFile);
					importFile = null;
//					break;
				default:
					trace("unsupported import format");
					break;
				}
			}	
  
			//Show the single preview when double clicking
			private function singleShow(evt:Event):void {
				singleSet.dataProvider = evt.currentTarget.selectedItem.data;
				//singleSetHex.dataProvider = evt.currentTarget.selectedItem.data;
				currentState = 'singleView';
			}
			
			private function checkText():void {
				
			}
		]]>
	</mx:Script>
	
	<mx:XML id="menuItems" xmlns="">
		<root>
			<FileMenu label='File'>
				<Open label='Import ASE'/>
				<Save label='Export ASE'/>
				<Import label='Restore Collection'/>
				<Export label='Backup Collection'/>
			</FileMenu>
		</root>
	</mx:XML>
	
	<mx:Canvas id="main" width="100%" height="100%" top="0" left="0" horizontalScrollPolicy="off" verticalScrollPolicy="off">
		<mx:VBox top="10" left="10" bottom="10" right="10" horizontalAlign="center">
			<mx:HBox width="100%" verticalAlign="middle">
				<mx:Button label="New" icon="@Embed('images/new.png')" height="22" click="addItem()"/>
				<mx:Button label="Edit" enabled="{colorDisplayList.selectedItem}" click="editItem()" icon="@Embed('images/edit-colors.png')"/>
				<mx:Button label="Duplicate" enabled="{colorDisplayList.selectedItem}" click="duplicateItem()" icon="@Embed('images/duplicate.png')"/>
				<mx:Button label="Delete" click="deleteItem()" icon="@Embed('images/delete.png')" height="22" toolTip="Delete" enabled="{colorDisplayList.selectedItem}"/>
			</mx:HBox>
			<mx:TileList id="colorDisplayList" width="100%" height="100%" dataProvider="{colorListCollection}" itemRenderer="com.levitation.ColorSetRenderer" columnWidth="120" dragEnabled="true" dropEnabled="true" dragMoveEnabled="true" doubleClickEnabled="true" doubleClick="singleShow(event)"/>
		</mx:VBox>
		<comp:ColorEdit id="editBox" horizontalCenter="0" verticalCenter="-30" visible="false"/>
		<mx:Canvas id="singleView" top="5" left="5" bottom="5" right="5" styleName="singlePreview" visible="false" showEffect="Fade" hideEffect="Fade">
			<comp:ColorSet id="singleSet" left="0" right="0" top="0" bottom="40"/>
			<!--<comp:ColorSetHex id="singleSetHex" left="0" right="0" bottom="40"/>-->
			<mx:Button id="closeSingle" label="Close" click="{currentState = ''}" horizontalCenter="0" bottom="10"/>
		</mx:Canvas>
	</mx:Canvas>
</mx:WindowedApplication>