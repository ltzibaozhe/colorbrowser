<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:comp="components.*" layout="absolute" xmlns:filters="flash.filters.*" xmlns:controls="flexlib.controls.*" applicationComplete="onComplete()" width="720" height="600">
	<mx:Style source="css/colorswatch.css"/>
	<mx:Script>
		<![CDATA[
			import mx.events.CloseEvent;
			import mx.events.CollectionEvent;
			import com.sophware.Util;
			import flash.events.InvokeEvent;
			import flash.desktop.DragManager;
			import components.ColorEdit;
			import com.levitation.AirUtil;
			import mx.controls.Alert;
			import mx.managers.CursorManager;
			import mx.events.DragEvent;
			import mx.events.ResizeEvent;
			import flash.filesystem.FileMode;
			import flash.filesystem.File;
			import flash.filesystem.FileStream;
			import flash.events.IOErrorEvent;
			import mx.events.FlexEvent;
			import mx.collections.ArrayCollection;
			import mx.core.Container;
			import mx.core.UIComponent;
			
			private var winUtil:AirUtil;
			private var lastString:String;
			private var colorFile:File; //Save location for colors
			private var colorFileStream:FileStream;
			private var saveFile:File; //Save file reference
			private var saveFileStream:FileStream;
			private var inFile:File; //File to open, replaces current file
			private var inFileStream:FileStream;
			private var dragFile:File; //File dropped on app
			private var dragFileStream:FileStream;
			
			private var invokeVar:Boolean;
			
			//Variables to control filter dropshadows
			[Bindable]
			private var dropBlur:Number = 3;
			[Bindable]
			private var dropDistance:Number = 1;
			[Bindable]
			private var dropAlpha:Number = 0.65;
			
			[Bindable]
			private var colorCollection:ArrayCollection;
			[Bindable]
			public var colorListCollection:ArrayCollection = new ArrayCollection();
			
			private function onComplete():void {
				//Remember window size and location
				winUtil = new AirUtil(stage);
				
				//Set up file access
				colorFile = File.applicationStorageDirectory.resolvePath("saved.color");
				
				//File exists, use it
				if(colorFile.exists  && colorFile.size > 10) {
					colorFileStream = new FileStream();
					colorFileStream.open(colorFile, FileMode.UPDATE);
					colorListCollection = colorFileStream.readObject() as ArrayCollection;
				}
				//Need to create a new file and start using it
				else {
					var tempFileStream:FileStream = new FileStream();
					tempFileStream.open(colorFile,FileMode.WRITE);
					tempFileStream.close();
					colorFileStream = new FileStream();
					colorFileStream.open(colorFile, FileMode.UPDATE);
				}
				
				//Set up app to accept drag
				colorDisplayList.addEventListener("nativeDragOver",fileDrag);
				colorDisplayList.addEventListener("nativeDragDrop",acceptDrag);
				
				Shell.shell.addEventListener(InvokeEvent.INVOKE,onInvokeEvent);
				
				//Listen for collection changes
				colorListCollection.addEventListener(CollectionEvent.COLLECTION_CHANGE,collectionUpdate);
			}
			
			//Referenced when a .color file is double clicked in the OS
			private function onInvokeEvent(evt:InvokeEvent):void {
				var tempFile:File;
				
				singleView.visible = false;
				
				for each(var tempString:String in evt.arguments) {
					try {
						tempFile = new File(tempString);
						if(tempFile.extension == "color") {
							//TODO: Use prompt when opening a file with the open button also.
							inFile = tempFile;
							Alert.show("Do you want to save your current color set?","Alert",Alert.YES|Alert.NO,main,handleFile,null,Alert.YES);
						}
					}
					catch(err:Error) {
						trace(err.message);
					}
				}
			}
			
			private function handleFile(evt:CloseEvent):void {
				if(evt.detail == Alert.YES) {
					invokeVar = true;
					outFileClick();
				}
				else {
					copyFile(new Event("bogus"));
				}
			}
			
			private function collectionUpdate(evt:CollectionEvent):void {
				writeCollection(colorListCollection);
			}
			
			private function renderColor(inCollection:Object):void {
				colorCollection = inCollection.data as ArrayCollection;
			}
			
			private function setColor(inObject:Container,rgb:String):void {
				inObject.setStyle("backgroundColor",rgb);
			}
			
			private function addItem():void {
				editBox.visible = true;
				editBox.colorObject = {name:"New Color",data:new ArrayCollection([0,0,0,0,0])};
				editBox.editing = false;
			}
			
			private function deleteItem():void {
				colorListCollection.removeItemAt(colorDisplayList.selectedIndex);
				writeCollection(colorListCollection);
			}
			
			private function duplicateItem():void {
				colorListCollection.addItem(colorDisplayList.selectedItem.valueOf());
			}
			
			private function editItem():void {
				editBox.visible = true;
				editBox.colorObject = colorDisplayList.selectedItem;
				editBox.editing = true;
			}
			
			private function writeCollection(inCollection:ArrayCollection):void {
				colorFileStream.position = 0;
				colorFileStream.truncate();
				colorFileStream.writeObject(inCollection);
			}
			
			private function toClipBoard():void {
				System.setClipboard(colorDisplayList.selectedItem.string);
			}
			
			private function outFileClick():void {
				try {
					saveFile = File.documentsDirectory;
					saveFile.browseForSave("Save Colors");
					saveFile.addEventListener(Event.SELECT, saveColors);
				}
				catch (error:Error) {
					Alert.show("Failed: "+error.message,"Save failed");
				}
			}
			
			private function saveColors(evt:Event):void {
				var outFile:File = evt.target as File;
				var outFileStream:FileStream = new FileStream();
				outFileStream.open(outFile,FileMode.WRITE);
				outFileStream.writeObject(colorListCollection);
				outFileStream.close();
				CursorManager.removeBusyCursor();
				if(invokeVar) {
					copyFile(new Event("bogus"));
				}
			}
			
			private function inFileClick():void {
				inFile = new File();
				inFile.addEventListener( Event.SELECT, copyFile );
				inFile.browse( new Array( new FileFilter( "Color Files", "*.color" ) ) );
			}
			private function copyFile(evt:Event):void {
				colorFileStream.close();
				inFile.copyTo(colorFile,true);
				colorFileStream.open(colorFile, FileMode.UPDATE);
				colorListCollection = colorFileStream.readObject() as ArrayCollection;
			}
			
			private function closeApp():void {
				colorFileStream.close();
				stage.nativeWindow.close();
			}
			
			private function fileDrag(evt:NativeDragEvent):void {
				if(evt.clipboard.hasFormat(ClipboardFormats.FILE_LIST_FORMAT)){
					DragManager.acceptDragDrop(colorDisplayList);
				}
			}
			
			private function acceptDrag(evt:NativeDragEvent):void {
				DragManager.dropAction = "copy";
				var tempArray:Array = evt.clipboard.dataForFormat(ClipboardFormats.FILE_LIST_FORMAT) as Array;
				dragFileStream = new FileStream();
				for each(var tempFile:File in tempArray) {
					if(tempFile.extension == "css") {
						translateColourLover(tempFile);
					}
					else if(tempFile.extension == "ase") {
						translateAse(tempFile);
					}
					else {
						Alert.show("Sorry, I don't recognize that file format.");
					}
				}
			}
			
			//Takes a downloaded ColourLovers CSS file and creates a color set
			private function translateColourLover(inFile:File):void {
				dragFileStream.open(inFile,FileMode.READ);
				var tempString:String = dragFileStream.readMultiByte(dragFileStream.bytesAvailable,"us-ascii");
				dragFileStream.close();
				
				if(tempString.indexOf("/* COLOURlovers.com") != -1) {
					var tempArray:Array = tempString.split(".color");
					var tempName:String = tempArray[0].substring(22,tempArray[0].length-5);
					var tempColors:Array = new Array();
					for(var i:int=1;i<tempArray.length;i++) {
						tempColors.push(uint("0x"+tempArray[i].substr(1,6)));
					}
					colorListCollection.addItem({data:new ArrayCollection(tempColors),name:tempName,string:tempString});
				}
				else {
					Alert.show("Not a valid ColourLovers CSS file","Error");
				}
			}
			
			//To use when translating an ASE file, currently very incomplete
			private function translateAse(inFile:File):void {
				trace("Path: "+inFile.nativePath);
				dragFileStream.open(inFile,FileMode.READ);
				//trace(dragFileStream.bytesAvailable);
				var tempBytes:ByteArray = new ByteArray();
				dragFileStream.readBytes(tempBytes,0,dragFileStream.bytesAvailable);
				dragFileStream.close();
				
				var tempNum:Number = tempBytes.bytesAvailable;
				var colorSetArray:Array = new Array();
				
				for(var i:int=0; i< tempNum-3; i++) {
					tempBytes.position = i;
					//trace(tempBytes.position);
					if(tempBytes.readUTFBytes(3) == "RGB") {
						tempBytes.position++;
						trace(tempBytes.position.toString(16));
						trace("Start of color");
						
						var shortArray:Array = new Array();
						for(var j:int=0;j<3;j++) {
							shortArray.push(tempBytes.readFloat()*255);
						}
						var colorVal:uint = (shortArray[0]<<16) | (shortArray[1]<<8) | shortArray[2];
						trace(colorVal.toString(16));
						colorSetArray.push(colorVal);
						
						i = tempBytes.position-1;
					}
					
					editBox.visible = true;
					editBox.colorObject = {name:"New ASE Color",data:new ArrayCollection(colorSetArray)};
				}
			}
			
			//Show the single preview when double clicking
			private function singleShow(evt:Event):void {
				singleSet.dataProvider = evt.currentTarget.selectedItem.data;
				singleSetHex.dataProvider = evt.currentTarget.selectedItem.data;
				singleView.visible = true;
			}
		]]>
	</mx:Script>
	<mx:Canvas id="main" width="100%" height="100%" top="0" left="0" horizontalScrollPolicy="off" verticalScrollPolicy="off">
		<mx:VBox top="10" left="10" bottom="10" right="10" horizontalAlign="center">
			<mx:HBox width="100%" verticalAlign="middle">
				<mx:Button label="Open" icon="@Embed('images/document-open.png')" height="22" click="inFileClick()"/>
				<mx:Button label="Save" icon="@Embed('images/save.png')" height="22" click="outFileClick()"/>
				<mx:VRule height="15"/>
				<mx:Button label="New" icon="@Embed('images/new.png')" height="22" click="addItem()"/>
				<mx:Button label="From Image" height="22"/>
				<mx:Button label="Edit" enabled="{colorDisplayList.selectedItem}" click="editItem()" icon="@Embed('images/edit-colors.png')"/>
				<mx:Button label="Duplicate" enabled="{colorDisplayList.selectedItem}" click="duplicateItem()" icon="@Embed('images/duplicate.png')"/>
				<mx:Button label="Delete" click="deleteItem()" icon="@Embed('images/delete.png')" height="22" toolTip="Delete" enabled="{colorDisplayList.selectedItem}"/>
			</mx:HBox>
			<mx:TileList id="colorDisplayList" width="100%" height="100%" dataProvider="{colorListCollection}" itemRenderer="components.ColorSetRenderer" columnWidth="120" dragEnabled="true" dropEnabled="true" dragMoveEnabled="true" doubleClickEnabled="true" doubleClick="singleShow(event)"/>
		</mx:VBox>
		<comp:ColorEdit id="editBox" horizontalCenter="0" verticalCenter="-30" visible="false"/>
		<mx:Canvas id="singleView" top="5" left="5" bottom="5" right="5" styleName="singlePreview" visible="false" showEffect="Fade" hideEffect="Fade">
			<comp:ColorSet id="singleSet" left="0" right="0" top="0" bottom="40"/>
			<comp:ColorSetHex id="singleSetHex" left="0" right="0" bottom="40"/>
			<mx:Button id="closeSingle" label="Close" click="{singleView.visible=false}" horizontalCenter="0" bottom="10"/>
		</mx:Canvas>
	</mx:Canvas>
</mx:WindowedApplication>
